= SELECT Syntax
:idprefix: _

[#select]
`select::= select-term [ set-op [ ALL ] ] [ order-by-clause | limit-clause | offset-clause ]`

.Railroad Diagram: select
image::n1ql-language-reference/select.png[]

[#select-term]
`select-term::= subselect | "(" select ")"`

.Railroad Diagram: select-term
image::n1ql-language-reference/select-term.png[]

[#subselect]
`subselect::= select-from | from-select`

.Railroad Diagram: subselect
image::n1ql-language-reference/subselect.png[]

[#select-from]
`select-from::= select-clause [ from-clause | let-clause | where-clause | group-by-clause ]`

.Railroad Diagram: select-from
image::n1ql-language-reference/select-from.png[]

[#from-select]
`from-select::= from-clause [ let-clause ] [ where-clause ] [ group-by-clause ] select-clause`

.Railroad Diagram: from-select
image::n1ql-language-reference/from-select.png[]

== SELECT Clause

[#select-clause]
`select-clause::= SELECT [ ALL | DISTINCT ] ( result-expr [ , result-expr ]* | ( RAW | ELEMENT | VALUE ) expr [ [AS] alias ] )`

.Railroad Diagram: select-clause
image::n1ql-language-reference/select-clause.png[]

== FROM Clause

[#from-clause]
`from-clause::= FROM from-term`

.Railroad Diagram: from-clause
image::n1ql-language-reference/from-clause.png[]

[#from-term]
`from-term::= from-keyspace [ [ AS ] alias ] [ use-clause ] | "(" select ")" [ AS ] alias | expr [ [ AS ] alias] | from-term ( join-clause | nest-clause | unnest-clause )`

.Railroad Diagram: from-term
image::n1ql-language-reference/from-term.png[]

[#from-keyspace]
`from-keyspace::= [ namespace : ] keyspace`

.Railroad Diagram: from-keyspace
image::n1ql-language-reference/from-keyspace.png[]

== JOIN Clause

[#join-clause]
`join-clause::= [ join-type ] JOIN from-keyspace [ [ AS ] alias ] join-predicate`

.Railroad Diagram: join-clause
image::n1ql-language-reference/join-clause.png[]

[#join-type]
`join-type::= INNER |  LEFT [ OUTER ]`

.Railroad Diagram: join-type
image::n1ql-language-reference/join-type.png[]

[#join-predicate]
`join-predicate::= lookup-join-predicate | index-join-predicate`

.Railroad Diagram: join-predicate
image::n1ql-language-reference/join-predicate.png[]

[#lookup-join-predicate]
`lookup-join-predicate::= ON [ PRIMARY ] KEYS expr`

.Railroad Diagram: lookup-join-predicate
image::n1ql-language-reference/lookup-join-predicate.png[]

[#index-join-predicate]
`index-join-predicate::= ON [ PRIMARY ] KEY expr FOR alias`

.Railroad Diagram: index-join-predicate
image::n1ql-language-reference/index-join-predicate.png[]

== NEST Clause

[#nest-clause]
`nest-clause::= [ join-type ] NEST from-keyspace [ [ AS ] alias ] join-predicate`

.Railroad Diagram: nest-clause
image::n1ql-language-reference/nest-clause.png[]

== UNNEST Clause

[#unnest-clause]
`unnest-clause::= [ join-type ] ( UNNEST | FLATTEN ) expr [ [ AS ] alias ]`

.Railroad Diagram: unnest-clause
image::n1ql-language-reference/unnest-clause.png[]

== USE Clause

[#use-clause]
`use-clause::= use-keys-clause | use-index-clause`

.Railroad Diagram: use-clause
image::n1ql-language-reference/use-clause.png[]

[#use-keys-clause]
`use-keys-clause::= USE [ PRIMARY ] KEYS expr`

.Railroad Diagram: use-keys-clause
image::n1ql-language-reference/use-keys-clause.png[]

[#use-index-clause]
`use-index-clause::= USE INDEX "(" index-ref [ "," index-ref ]* ")"`

.Railroad Diagram: use-index-clause
image::n1ql-language-reference/use-index-clause.png[]

[#index-ref]
`index-ref::= index-name [ index-using ]`

.Railroad Diagram: index-ref
image::n1ql-language-reference/index-ref.png[]

[#index-name]
`index-name::= identifier`

.Railroad Diagram: index-name
image::n1ql-language-reference/index-name.png[]

[#index-using]
`index-using::= USING ( VIEW | GSI )`

.Railroad Diagram: index-using
image::n1ql-language-reference/index-using.png[]

(Note that USING VIEW is deprecated and will be removed in a future release.)

== LET Clause

[#let-clause]
`let-clause::= LET alias "=" expr [ "," alias = expr ]*`

.Railroad Diagram: let-clause
image::n1ql-language-reference/let-clause.png[]

== WHERE Clause

[#where-clause]
`where-clause::= WHERE cond`

.Railroad Diagram: where-clause
image::n1ql-language-reference/where-clause.png[]

[#cond]
`cond::= expr`

.Railroad Diagram: cond
image::n1ql-language-reference/cond.png[]

== GROUP BY Clause

[#group-by-clause]
`group-by-clause::= GROUP BY expr [ "," expr ]* [ letting-clause ] [ having-clause ] | letting-clause`

.Railroad Diagram: group-by-clause
image::n1ql-language-reference/group-by-clause.png[]

[#letting-clause]
`letting-clause::= LETTING alias "=" expr [ "," alias = expr ]*`

.Railroad Diagram: letting-clause
image::n1ql-language-reference/letting-clause.png[]

[#having-clause]
`having-clause::= HAVING cond`

.Railroad Diagram: having-clause
image::n1ql-language-reference/having-clause.png[]

[#set-op]
`set-op::= UNION | INTERSECT | EXCEPT`

.Railroad Diagram: set-op
image::n1ql-language-reference/set-op.png[]

== ORDER BY Clause

[#order-by-clause]
`order-by-clause::= ORDER BY ordering-term [ "," ordering-term ]*`

.Railroad Diagram: order-by-clause
image::n1ql-language-reference/order-by-clause.png[]

[#ordering-term]
`ordering-term::= expr [ ASC | DESC ]`

.Railroad Diagram: ordering-term
image::n1ql-language-reference/ordering-term.png[]

== LIMIT Clause

[#limit-clause]
`limit-clause::= LIMIT expr`

.Railroad Diagram: limit-clause
image::n1ql-language-reference/limit-clause.png[]

[#offset-clause]
`offset-clause::= OFFSET expr`

.Railroad Diagram: offset-clause
image::n1ql-language-reference/offset-clause.png[]
