= SELECT Syntax
:description: This page enables you to drill down through the syntax of a SELECT query.
:idprefix: _
:imagesdir: ../../assets/images

:expression: xref:n1ql-language-reference/index.adoc#N1QL_Expressions
:hints: xref:n1ql-language-reference/optimizer-hints.adoc
:conventions: xref:n1ql-language-reference/conventions.adoc
:number: xref:n1ql-language-reference/literals.adoc#numbers
:identifier: xref:n1ql-language-reference/identifiers.adoc
:alias: {identifier}#identifier-alias

[abstract]
{description}

[#select,reftext="select",subs="normal"]
----
select ::= <<select-term>> [ <<set-op>> <<select-term>> ]* [ <<order-by-clause>> ] [ <<limit-clause>> ] [ <<offset-clause>> ]
----

image::n1ql-language-reference/select.png[]

[#select-term,reftext="select-term",subs="normal"]
----
select-term ::= <<subselect>> | '(' <<select>> ')'
----

image::n1ql-language-reference/select-term.png[]

[#subselect,reftext="subselect",subs="normal"]
----
subselect ::= <<select-from>> | <<from-select>>
----

image::n1ql-language-reference/subselect.png[]

[#select-from,reftext="select-from",subs="normal"]
----
select-from ::= [ <<with-clause>> ] <<select-clause>> [ <<from-clause>> ] [ <<let-clause>> ] [ <<where-clause>> ]
                [ <<group-by-clause>> ] [ <<window-clause>> ]
----

image::n1ql-language-reference/select-from.png[]

[#from-select,reftext="from-select",subs="normal"]
----
from-select ::= [ <<with-clause>> ] <<from-clause>> [ <<let-clause>> ] [ <<where-clause>> ] [ <<group-by-clause>> ]
                [ <<window-clause>> ] <<select-clause>>
----

image::n1ql-language-reference/from-select.png[]

[#set-op,reftext="set-op",subs="normal"]
----
set-op ::= ( UNION | INTERSECT | EXCEPT ) [ ALL ]
----

image::n1ql-language-reference/set-op.png[]

[[with-clause,with-clause]]
== WITH Clause

[subs="normal"]
----
with-clause ::= WITH {alias}[alias] AS '(' ( <<select>> | {expression}[expression] ) ')'
               [ ',' {alias}[alias] AS '(' ( <<select>> | {expression}[expression] ) ')' ]*
----

image::n1ql-language-reference/with-clause.png[]

[[select-clause,select-clause]]
== SELECT Clause

[subs="normal"]
----
select-clause ::= SELECT [ {hints}[hint-comment] ] <<projection>>
----

image::n1ql-language-reference/select-clause.png["'SELECT' hint-comment? projection"]

[#projection,reftext="projection",subs="normal"]
----
projection ::= [ ALL | DISTINCT ]
               ( <<result-expr>> [ , <<result-expr>> ]* | ( RAW | ELEMENT | VALUE ) {expression}[expr] [ [ AS ] {alias}[alias] ] )
----

image::n1ql-language-reference/projection.png["( 'ALL' | 'DISTINCT' )? ( result-expr ( ',' result-expr )* | ( 'RAW' | 'ELEMENT' | 'VALUE' ) expr ( 'AS'? alias )? )"]

[#result-expr,reftext="result-expr",subs="normal"]
----
result-expr ::= ( [ <<path>> "." ] "*" | {expression}[expr] [ [ AS ] {alias}[alias] ] )
----

image::n1ql-language-reference/result-expr.png["( path '.' )? '*' | expr ( 'AS'? alias )?"]

[#path,reftext="path",subs="normal"]
----
path ::= {identifier}[identifier] [ '[' {expression}[expr] ']' ]* [ '.' <<path>> ]
----

image::n1ql-language-reference/path.png["identifier ( '[' expr ']' )* ( '.' path )?"]

[[from-clause,from-clause]]
== FROM Clause

[subs="normal"]
----
from-clause ::= FROM <<from-term>>
----

image::n1ql-language-reference/from-clause.png[]

[#from-term,reftext="from-terms",subs="normal"]
----
from-terms ::= ( <<from-keyspace>> | <<from-subquery>> | <<from-generic>> )
               ( <<join-clause>> | <<nest-clause>> | <<unnest-clause>> )*  <<comma-separated-join>>*
----

image::n1ql-language-reference/from-terms.png["( from-keyspace | from-subquery | from-generic ) ( join-clause | nest-clause | unnest-clause )* comma-separated-join*"]

[#from-keyspace,reftext="from-keyspace",subs="normal"]
----
from-keyspace ::= <<keyspace-ref>> [ [ AS ] {alias}[alias] ] [ <<use-clause>> ]
----

image::n1ql-language-reference/from-keyspace.png[]

[#keyspace-ref,reftext="keyspace-ref",subs="normal"]
----
keyspace-ref ::= <<keyspace-path>> | <<keyspace-partial>>
----

image::n1ql-language-reference/keyspace-ref.png["keyspace-path | keyspace-partial"]

[#keyspace-path,reftext="keyspace-path",subs="normal"]
----
keyspace-path ::= [ <<namespace>> ':' ] <<bucket>> [ '.' <<scope>> '.' <<collection>> ]
----

image::n1ql-language-reference/keyspace-path.png["( namespace ':' )? bucket ( '.' scope '.' collection )?"]

[#keyspace-partial,reftext="keyspace-partial",subs="normal"]
----
keyspace-partial ::= <<collection>>
----

image::n1ql-language-reference/keyspace-partial.png["collection"]

[#namespace,reftext="namespace",subs="normal"]
----
namespace ::= {identifier}[identifier]
----

image::n1ql-language-reference/namespace.png[]

[#bucket,reftext="bucket",subs="normal"]
----
bucket ::= {identifier}[identifier]
----

image::n1ql-language-reference/keyspace.png[]

[#scope,reftext="scope",subs="normal"]
----
scope ::= {identifier}[identifier]
----

image::n1ql-language-reference/keyspace.png[]

[#collection,reftext="collection",subs="normal"]
----
collection ::= {identifier}[identifier]
----

image::n1ql-language-reference/keyspace.png[]

[#from-subquery,reftext="from-subquery",subs="normal"]
----
from-subquery ::= <<subquery-expr>> [ AS ] {alias}[alias]
----

image::n1ql-language-reference/select-expr.png[]

[#subquery-expr,reftext="subquery-expr",subs="normal"]
----
subquery-expr ::= '(' <<select>> ')'
----

image::n1ql-language-reference/subquery-expr.png[]

[#from-generic,reftext="from-generic",subs="normal"]
----
from-generic ::= {expression}[expr] [ AS {alias}[alias] ]
----

image::n1ql-language-reference/generic-expr.png[]

[[join-clause,join-clause]]
== JOIN Clause

[subs="normal"]
----
join-clause ::= <<ansi-join-clause>> | <<lookup-join-clause>> | <<index-join-clause>>
----

image::n1ql-language-reference/join-clause.png[]

[[ansi-join-clause,ansi-join-clause]]
=== ANSI JOIN

[subs="normal"]
----
ansi-join-clause ::= [ <<ansi-join-type>> ] JOIN <<ansi-join-rhs>> <<ansi-join-predicate>>
----

image::n1ql-language-reference/ansi-join-clause.png["ansi-join-type? 'JOIN' ansi-join-rhs ansi-join-predicate"]

[#ansi-join-type,reftext="ansi-join-type",subs="normal"]
----
ansi-join-type ::= INNER | ( LEFT [ OUTER ] ) | ( RIGHT [ OUTER ] )
----

image::n1ql-language-reference/ansi-join-type.png[]

[#ansi-join-rhs,reftext="ansi-join-rhs",subs="normal"]
----
ansi-join-rhs ::= <<rhs-keyspace>> | <<rhs-subquery>> | <<rhs-generic>>
----

image::n1ql-language-reference/ansi-join-rhs.png["rhs-keyspace | rhs-subquery | rhs-generic"]

[#rhs-keyspace,reftext="rhs-keyspace",subs="normal"]
----
rhs-keyspace ::= <<keyspace-ref>> [ [ AS ] {alias}[alias] ] [ <<ansi-join-hints>> ]
----

image::n1ql-language-reference/rhs-keyspace.png["keyspace-ref ( 'AS'? alias )? ansi-join-hints?"]

[#rhs-subquery,reftext="rhs-subquery",subs="normal"]
----
rhs-subquery ::= <<subquery-expr>> [ AS ] {alias}[alias]
----

image::n1ql-language-reference/rhs-subquery.png["subquery-expr 'AS'? alias"]

[#rhs-generic,reftext="rhs-generic",subs="normal"]
----
rhs-generic ::= {expression}[expr] [ [ AS ] {alias}[alias] ]
----

image::n1ql-language-reference/rhs-generic.png["expr ( 'AS'? alias )?"]

[#ansi-join-hints,reftext="ansi-join-hints",subs="normal"]
----
ansi-join-hints ::= <<use-hash-hint>> | <<use-nl-hint>> | <<multiple-hints>>
----

image::n1ql-language-reference/ansi-join-hints.png[]

[#use-hash-hint,reftext="use-hash-hint",subs="normal"]
----
use-hash-hint ::= USE <<use-hash-term>>
----

image::n1ql-language-reference/use-hash-hint.png[]

[#use-hash-term,reftext="use-hash-term",subs="normal"]
----
use-hash-term ::= HASH '(' ( BUILD | PROBE ) ')'
----

image::n1ql-language-reference/use-hash-term.png[]

[#use-nl-hint,reftext="use-nl-hint",subs="normal"]
----
use-nl-hint ::= USE <<use-nl-term>>
----

image::n1ql-language-reference/use-nl-hint.png[]

[#use-nl-term,reftext="use-nl-term",subs="normal"]
----
use-nl-term ::= NL
----

image::n1ql-language-reference/use-nl-term.png[]

[#multiple-hints,reftext="multiple-hints",subs="normal"]
----
multiple-hints ::= USE ( <<ansi-hint-terms>> <<other-hint-terms>> ) | ( <<other-hint-terms>> <<ansi-hint-terms>> )
----

image::n1ql-language-reference/multiple-hints.png[]

[#ansi-hint-terms,reftext="ansi-hint-terms",subs="normal"]
----
ansi-hint-terms ::= <<use-hash-term>> | <<use-nl-term>>
----

image::n1ql-language-reference/ansi-hint-terms.png[]

[#other-hint-terms,reftext="other-hint-terms",subs="normal"]
----
other-hint-terms ::= <<use-index-term>> | <<use-keys-term>>
----

image::n1ql-language-reference/other-hint-terms.png[]

[#ansi-join-predicate,reftext="ansi-join-predicate",subs="normal"]
----
ansi-join-predicate ::= ON {expression}[expr]
----

image::n1ql-language-reference/ansi-join-predicate.png[]

[[lookup-join-clause,lookup-join-clause]]
=== Lookup JOIN

[subs="normal"]
----
lookup-join-clause ::= [ <<lookup-join-type>> ] JOIN <<lookup-join-rhs>> <<lookup-join-predicate>>
----

image::n1ql-language-reference/lookup-join-clause.png["lookup-join-type? 'JOIN' lookup-join-rhs lookup-join-predicate"]

[#lookup-join-type,reftext="lookup-join-type",subs="normal"]
----
lookup-join-type ::= INNER | ( LEFT [ OUTER ] )
----

image::n1ql-language-reference/lookup-join-type.png[]

[#lookup-join-rhs,reftext="lookup-join-rhs",subs="normal"]
----
lookup-join-rhs ::= <<keyspace-ref>> [ [ AS ] {alias}[alias] ]
----

image::n1ql-language-reference/lookup-join-rhs.png["keyspace-ref ( 'AS'? alias )?"]

[#lookup-join-predicate,reftext="lookup-join-predicate",subs="normal"]
----
lookup-join-predicate ::= ON [ PRIMARY ] KEYS {expression}[expr]
----

image::n1ql-language-reference/lookup-join-predicate.png[]

[[index-join-clause,index-join-clause]]
=== Index JOIN

[subs="normal"]
----
index-join-clause ::= [ <<index-join-type>> ] JOIN <<index-join-rhs>> <<index-join-predicate>>
----

image::n1ql-language-reference/index-join-clause.png["index-join-type? 'JOIN' index-join-rhs index-join-predicate"]

[#index-join-type,reftext="index-join-type",subs="normal"]
----
index-join-type ::= INNER | ( LEFT [ OUTER ] )
----

image::n1ql-language-reference/index-join-type.png[]

[#index-join-rhs,reftext="index-join-rhs",subs="normal"]
----
index-join-rhs ::= <<keyspace-ref>> [ [ AS ] {alias}[alias] ]
----

image::n1ql-language-reference/index-join-rhs.png["keyspace-ref ( 'AS'? alias )?"]

[#index-join-predicate,reftext="index-join-predicate",subs="normal"]
----
index-join-predicate ::= ON [ PRIMARY ] KEY {expression}[expr] FOR {alias}[alias]
----

image::n1ql-language-reference/index-join-predicate.png[]

[[nest-clause,nest-clause]]
== NEST Clause

[subs="normal"]
----
nest-clause ::= <<ansi-nest-clause>> | <<lookup-nest-clause>> | <<index-nest-clause>>
----

image::n1ql-language-reference/nest-clause.png[]

[[ansi-nest-clause,ansi-nest-clause]]
=== ANSI NEST

[subs="normal"]
----
ansi-nest-clause ::= [ <<ansi-nest-type>> ] NEST <<ansi-nest-rhs>> <<ansi-nest-predicate>>
----

image::n1ql-language-reference/ansi-nest-clause.png["ansi-nest-type? 'NEST' ansi-nest-rhs ansi-nest-predicate"]

[#ansi-nest-type,reftext="ansi-nest-type",subs="normal"]
----
ansi-nest-type ::= INNER | ( LEFT [ OUTER ] )
----

image::n1ql-language-reference/ansi-nest-type.png[]

[#ansi-nest-rhs,reftext="ansi-nest-rhs",subs="normal"]
----
ansi-nest-rhs ::= <<keyspace-ref>> [ [ AS ] {alias}[alias] ]
----

image::n1ql-language-reference/ansi-nest-rhs.png["keyspace-ref ( 'AS'? alias )?"]

[#ansi-nest-predicate,reftext="ansi-nest-predicate",subs="normal"]
----
ansi-nest-predicate ::= ON {expression}[expr]
----

image::n1ql-language-reference/ansi-nest-predicate.png[]

[[lookup-nest-clause,lookup-nest-clause]]
=== Lookup NEST

[subs="normal"]
----
lookup-nest-clause ::= [ <<lookup-nest-type>> ] NEST <<lookup-nest-rhs>> <<lookup-nest-predicate>>
----

image::n1ql-language-reference/lookup-nest-clause.png["lookup-nest-type? 'NEST' lookup-nest-rhs lookup-nest-predicate"]

[#lookup-nest-type,reftext="lookup-nest-type",subs="normal"]
----
lookup-nest-type ::= INNER | ( LEFT [ OUTER ] )
----

image::n1ql-language-reference/lookup-nest-type.png[]

[#lookup-nest-rhs,reftext="lookup-nest-rhs",subs="normal"]
----
lookup-nest-rhs ::= <<keyspace-ref>> [ [ AS ] {alias}[alias] ]
----

image::n1ql-language-reference/lookup-nest-rhs.png["keyspace-ref ( 'AS'? alias )?"]

[#lookup-nest-predicate,reftext="lookup-nest-predicate",subs="normal"]
----
lookup-nest-predicate ::= ON KEYS {expression}[expr]
----

image::n1ql-language-reference/lookup-nest-predicate.png[]

[[index-nest-clause,index-nest-clause]]
=== Index NEST

[subs="normal"]
----
index-nest-clause ::= [ <<index-nest-type>> ] NEST <<index-nest-rhs>> <<index-nest-predicate>>
----

image::n1ql-language-reference/index-nest-clause.png["index-nest-type? 'NEST' index-nest-rhs index-nest-predicate"]

[#index-nest-type,reftext="index-nest-type",subs="normal"]
----
index-nest-type ::= INNER | ( LEFT [ OUTER ] )
----

image::n1ql-language-reference/index-nest-type.png[]

[#index-nest-rhs,reftext="index-nest-rhs",subs="normal"]
----
index-nest-rhs ::= <<keyspace-ref>> [ [ AS ] {alias}[alias] ]
----

image::n1ql-language-reference/index-nest-rhs.png["keyspace-ref ( 'AS'? alias )?"]

[#index-nest-predicate,reftext="index-nest-predicate",subs="normal"]
----
index-nest-predicate ::= ON KEY {expression}[expr] FOR {alias}[alias]
----

image::n1ql-language-reference/index-nest-predicate.png[]

[[unnest-clause,unnest-clause]]
== UNNEST Clause

[subs="normal"]
----
unnest-clause ::= [ <<unnest-type>> ] ( UNNEST | FLATTEN ) {expression}[expr] [ [ AS ] {alias}[alias] ]
----

image::n1ql-language-reference/unnest-clause.png[]

[#unnest-type,reftext="unnest-type",subs="normal"]
----
unnest-type ::= INNER | ( LEFT [ OUTER ] )
----

image::n1ql-language-reference/unnest-type.png[]

[[comma-separated-join,comma-separated-join]]
== Comma-Separated Join

[subs="normal"]
----
comma-separated-join ::= ',' ( <<rhs-keyspace>> | <<rhs-subquery>> | <<rhs-generic>> )
----

image::n1ql-language-reference/comma-separated-join.png["',' ( from-keyspace | from-subquery | from-generic )"]

[[use-clause,use-clause]]
== USE Clause

[subs="normal"]
----
use-clause ::= <<use-keys-clause>> | <<use-index-clause>>
----

image::n1ql-language-reference/use-clause.png[]

[#use-keys-clause,reftext="use-keys-clause",subs="normal"]
----
use-keys-clause ::= USE <<use-keys-term>>
----

image::n1ql-language-reference/use-keys-clause.png[]

[#use-keys-term,reftext="use-keys-term",subs="normal"]
----
use-keys-term ::= [ PRIMARY ] KEYS {expression}[expr]
----

image::n1ql-language-reference/use-keys-term.png[]

[#use-index-clause,reftext="use-index-clause",subs="normal"]
----
use-index-clause ::= USE <<use-index-term>>
----

image::n1ql-language-reference/use-index-clause.png[]

[#use-index-term,reftext="use-index-term",subs="normal"]
----
use-index-term ::= INDEX '(' <<index-ref>> [ ',' <<index-ref>> ]* ')'
----

image::n1ql-language-reference/use-index-term.png[]

[#index-ref,reftext="index-ref",subs="normal"]
----
index-ref ::= [ <<index-name>> ] [ <<index-type>> ]
----

image::n1ql-language-reference/index-ref.png[]

[#index-name,reftext="index-name",subs="normal"]
----
index-name ::= {identifier}[identifier]
----

image::n1ql-language-reference/index-name.png[]

[#index-type,reftext="index-type",subs="normal"]
----
index-type ::= USING ( GSI | FTS )
----

image::n1ql-language-reference/index-type.png["'USING' ( 'GSI' | 'FTS' )"]

[[let-clause,let-clause]]
== LET Clause

[subs="normal"]
----
let-clause ::= LET {alias}[alias] '=' {expression}[expr] [ ',' {alias}[alias] '=' {expression}[expr] ]*
----

image::n1ql-language-reference/let-clause.png[]

[[where-clause,where-clause]]
== WHERE Clause

[subs="normal"]
----
where-clause ::= WHERE <<cond>>
----

image::n1ql-language-reference/where-clause.png[]

[#cond,reftext="cond",subs="normal"]
----
cond ::= {expression}[expr]
----

image::n1ql-language-reference/cond.png[]

[[group-by-clause,group-by-clause]]
== GROUP BY Clause

[subs="normal"]
----
group-by-clause ::= GROUP BY {expression}[expr] [ ',' {expression}[expr] ]* [ <<letting-clause>> ] [ <<having-clause>> ] | <<letting-clause>>
----

image::n1ql-language-reference/group-by-clause.png[]

[#letting-clause,reftext="letting-clause",subs="normal"]
----
letting-clause ::= LETTING {alias}[alias] '=' {expression}[expr] [ ',' {alias}[alias] '=' {expression}[expr] ]*
----

image::n1ql-language-reference/letting-clause.png[]

[#having-clause,reftext="having-clause",subs="normal"]
----
having-clause ::= HAVING <<cond>>
----

image::n1ql-language-reference/having-clause.png[]

[[window-clause,window-clause]]
== WINDOW Clause

[subs="normal"]
----
window-clause ::= WINDOW <<window-declaration>> [ ',' <<window-declaration>> ]*
----

image::n1ql-language-reference/window-clause.png[]

[#window-declaration,reftext="window-declaration",subs="normal"]
----
window-declaration ::= <<window-name>> AS '(' <<window-definition>> ')'
----

image::n1ql-language-reference/window-declaration.png[]

[#window-name,reftext="window-name",subs="normal"]
----
window-name ::= {identifier}[identifier]
----

image::n1ql-language-reference/window-name.png[]

[#window-definition,reftext="window-definition",subs="normal"]
----
window-definition ::= [ <<window-ref>> ] [ <<window-partition-clause>> ] [ <<window-order-clause>> ]
                      [ <<window-frame-clause>> ]
----

image::n1ql-language-reference/window-definition.png[]

[#window-ref,reftext="window-ref",subs="normal"]
----
window-ref ::= {identifier}[identifier]
----

image::n1ql-language-reference/window-ref.png[]

[#window-partition-clause,reftext="window-partition-clause",subs="normal"]
----
window-partition-clause ::= PARTITION BY {expression}[expr] [ ',' {expression}[expr] ]*
----

image::n1ql-language-reference/window-partition-clause.png[]

[#window-order-clause,reftext="window-order-clause",subs="normal"]
----
window-order-clause ::= ORDER BY <<ordering-term>> [ ',' <<ordering-term>> ]*
----

image::n1ql-language-reference/window-order-clause.png[]

[#window-frame-clause,reftext="window-frame-clause",subs="normal"]
----
window-frame-clause ::= ( ROWS | RANGE | GROUPS ) <<window-frame-extent>> [ <<window-frame-exclusion>> ]
----

image::n1ql-language-reference/window-frame-clause.png[]

[#window-frame-extent,reftext="window-frame-extent",subs="normal"]
----
window-frame-extent ::= UNBOUNDED PRECEDING | {number}[valexpr] PRECEDING | CURRENT ROW |
                        BETWEEN ( UNBOUNDED PRECEDING | CURRENT ROW |
                                  {number}[valexpr] ( PRECEDING | FOLLOWING ) )
                            AND ( UNBOUNDED FOLLOWING | CURRENT ROW |
                                  {number}[valexpr] ( PRECEDING | FOLLOWING ) )
----

image::n1ql-language-reference/window-frame-extent.png[]

[#window-frame-exclusion,reftext="window-frame-exclusion",subs="normal"]
----
window-frame-exclusion ::= EXCLUDE ( CURRENT ROW | GROUP | TIES | NO OTHERS )
----

image::n1ql-language-reference/window-frame-exclusion.png[]

[[order-by-clause,order-by-clause]]
== ORDER BY Clause

[subs="normal"]
----
order-by-clause ::= ORDER BY <<ordering-term>> [ ',' <<ordering-term>> ]*
----

image::n1ql-language-reference/order-by-clause.png[]

[#ordering-term,reftext="ordering-term",subs="normal"]
----
ordering-term::= {expression}[expr] [ ASC | DESC ] [ NULLS ( FIRST | LAST ) ]
----

image::n1ql-language-reference/ordering-term.png[]

[[limit-clause,limit-clause]]
== LIMIT Clause

[subs="normal"]
----
limit-clause ::= LIMIT {expression}[expr]
----

image::n1ql-language-reference/limit-clause.png[]

[[offset-clause,offset-clause]]
== OFFSET Clause

[subs="normal"]
----
offset-clause ::= OFFSET {expression}[expr]
----

image::n1ql-language-reference/offset-clause.png[]

== Related Links

* {conventions}[Conventions]