/* Utility Statements */

utility-statement ::= advise
                    | explain
                    | infer
                    | prepare
                    | execute
                    | update-statistics

/* tag::advise[] */
advise ::= 'ADVISE' 'INDEX'? ( select | update | delete | merge )
/* end::advise[] */

/* tag::explain[] */
explain ::= 'EXPLAIN' statement
/* end::explain[] */

/* tag::infer[] */
infer ::= 'INFER' ( 'COLLECTION' | 'KEYSPACE' )? keyspace-ref ( 'WITH' options )?
/* end::infer[] */

options ::= object-constructor

/***********************
 * Prepared Statements *
 ***********************/

/* tag::prepare[] */
prepare ::= 'PREPARE' 'FORCE'? ( name ( 'FROM' | 'AS' ) )? statement
/* end::prepare[] */

/* tag::execute[] */
execute ::= 'EXECUTE' name ( 'USING' parameters )?
/* end::execute[] */

name ::= identifier
parameters ::= array-constructor | object-constructor

/**************
 * Statistics *
 **************/

/* tag::update-statistics[] */
update-statistics ::= update-statistics-expr | update-statistics-index |
                      updated-statistics-indexes | update-statistics-delete
/* end::update-statistics[] */

/* tag::update-statistics-expr[] */
update-statistics-expr ::= ( 'UPDATE' 'STATISTICS' 'FOR'? |
                             'ANALYZE' ( 'KEYSPACE' | 'COLLECTION')? )
                             keyspace-ref '(' index-key ( ',' index-key )* ')' index-with?
/* end::update-statistics-expr[] */

/* tag::update-statistics-index[] */
update-statistics-index ::= ( 'UPDATE' 'STATISTICS' 'FOR' | 'ANALYZE' )
                              index-clause index-using?  index-with?
/* end::update-statistics-index[] */

/* tag::index-clause[] */
index-clause ::= 'INDEX' ( index-path '.' index-name | index-name 'ON' keyspace-ref )
/* end::index-clause[] */

/* tag::update-statistics-indexes[] */
update-statistics-indexes ::= ( 'UPDATE' 'STATISTICS' 'FOR'? |
                                'ANALYZE' ( 'KEYSPACE' | 'COLLECTION')? )
                                keyspace-ref indexes-clause index-using? index-with?
/* end::update-statistics-indexes[] */

/* tag::indexes-clause[] */
indexes-clause ::= 'INDEX' ( '(' ( index-name ( ',' index-name )* | subquery-expr ) ')' | 'ALL')
/* end::indexes-clause[] */

/* tag::update-statistics-delete[] */
update-statistics-delete ::= ( 'UPDATE' 'STATISTICS' 'FOR'? |
                               'ANALYZE' ( 'KEYSPACE' | 'COLLECTION')? )
                               keyspace-ref delete-clause
/* end::update-statistics-delete[] */

/* tag::delete-clause[] */
delete-clause ::= 'DELETE' ( delete-expr | delete-all )
/* end::delete-clause[] */

/* tag::delete-expr[] */
delete-expr ::= 'STATISTICS'? '(' index-key ( ',' index-key )* ')'
/* end::delete-expr[] */

/* tag::delete-all[] */
delete-all ::= 'ALL' | 'STATISTICS'
/* end::delete-all[] */