/* SQL++ for Query (N1QL) */

/**************
 * Statements *
 **************/

query ::= statement ( ';' statement )* ';'?

statement ::= dcl-statement |
              ddl-statement |
              dml-statement |
              dql-statement |
              tcl-statement |
              utility-statement

dcl-statement ::= [https://github.com/couchbase/docs-server/blob/release/7.1/modules/n1ql/partials/grammar/dcl.ebnf]
ddl-statement ::= [https://github.com/couchbase/docs-server/blob/release/7.1/modules/n1ql/partials/grammar/ddl.ebnf]
dml-statement ::= [https://github.com/couchbase/docs-server/blob/release/7.1/modules/n1ql/partials/grammar/dml.ebnf]
dql-statement ::= [https://github.com/couchbase/docs-server/blob/release/7.1/modules/n1ql/partials/grammar/dql.ebnf]
tcl-statement ::= [https://github.com/couchbase/docs-server/blob/release/7.1/modules/n1ql/partials/grammar/tcl.ebnf]

utility-statement ::= [https://github.com/couchbase/docs-server/blob/release/7.1/modules/n1ql/partials/grammar/dcl.ebnf]


/***************
 * Expressions *
 ***************/

expr ::= literal |
         identifier |
         arithmetic-term |
         comparison-term |
         concatenation-term |
         logical-term |
         case-expr |
         collection-expr |
         construction-expr |
         nested-expr |
         function-call |
         subquery-expr |
         '(' expr ')'


/* Literal */

literal ::= string | number | boolean | null | missing

/* tag::string */
string ::= '"' char* '"' | "'" char* "'"
/* end::string[] */

/* tag::char */
char ::= unicode-character |
         '\' ('\' | '/' | 'b' | 'f' | 'n' | 'r' | 't' | 'u' hex hex hex hex )
/* end::char[] */

/* tag::hex */
hex ::= [0-9a-fA-F]
/* end::hex[] */

/* tag::number[] */
number ::= '-'? int frac? exp?
/* end::number[] */

/* tag::int[] */
int ::= [0-9] | [1-9] [0-9]+
/* end::int[] */

/* tag::frac[] */
frac ::= '.' [0-9]+
/* end::frac[] */

/* tag::exp[] */
exp ::= ('e' | 'E') ('-' | '+')? [0-9]+
/* end::exp[] */

/* tag::boolean[] */
boolean ::= 'TRUE' | 'FALSE'
/* end::boolean[] */

/* tag::null[] */
null ::= 'NULL'
/* end::null[] */

/* tag::missing[] */
missing ::= 'MISSING'
/* end::missing[] */


/* Identifier */

identifier ::= unescaped-identifier | escaped-identifier

/* tag::unescaped-identifier[] */
unescaped-identifier ::= [a-zA-Z_] ( [0-9a-zA-Z_$] )*
/* end::unescaped-identifier[] */

/* tag::escaped-identifier[] */
escaped-identifier ::= '`' char+ '`'
/* end::escaped-identifier[] */


/* Arithmetic Terms */

/* tag::arithmetic-term[] */
arithmetic-term ::= expr '+' expr |
                    expr '-' expr |
                    expr '*' expr |
                    expr '/' expr |
                    expr '%' expr |
                    '-' expr
/* end::arithmetic-term[] */


/* Comparison Terms */

comparison-term ::= relational-expr |
                    between-expr |
                    like-expr |
                    is-expr

/* tag::relational-expr[] */
relational-expr ::= expr '=' expr |
                    expr '==' expr |
                    expr '!=' expr |
                    expr '<>' expr |
                    expr '>' expr |
                    expr '>=' expr |
                    expr '<' expr |
                    expr '<=' expr
/* end::relational-expr[] */

/* tag::between-expr[] */
between-expr ::= expr 'NOT'? 'BETWEEN' expr 'AND' expr
/* end::between-expr[] */

/* tag::like-expr[] */
like-expr ::= expr 'NOT'? 'LIKE' expr
/* end::like-expr[] */

/* tag::is-expr[] */
is-expr ::= expr 'IS' 'NOT'? 'NULL' |
            expr 'IS' 'NOT'? 'MISSING' |
            expr 'IS' 'NOT'? 'VALUED'
/* end::is-expr[] */


/* Concatenation Terms */

/* tag::concatenation-term[] */
concatenation-term ::= expr '||' expr
/* end::concatenation-term[] */


/* Logical Terms */

logical-term ::= and | or | not

/* tag::and[] */
and ::= condition 'AND' condition
/* end::and[] */

/* tag::or[] */
or ::= condition 'OR' condition
/* end::or[] */

/* tag::not[] */
not ::= 'NOT' condition
/* end::not[] */


/* Case Expressions */

/* tag::case-expr[] */
case-expr ::= simple-case-expr | searched-case-expr
/* end::case-expr[] */

/* tag::simple-case-expr[] */
simple-case-expr ::= 'CASE' expr ('WHEN' expr 'THEN' expr)+ ('ELSE' expr)? 'END'
/* end::simple-case-expr[] */

/* tag::searched-case-expr[] */
searched-case-expr ::= 'CASE' ('WHEN' cond 'THEN' expr)+ ('ELSE' expr)? 'END'
/* end::searched-case-expr[] */


/* Collection Expressions */

collection-expr ::= exists-expr | in-expr | within-expr | range-cond | range-xform

/* tag::exists-expr[] */
exists-expr ::= 'EXISTS' expr
/* end::exists-expr[] */

/* tag::in-expr[] */
in-expr ::= search-expr 'NOT'? 'IN' target-expr
/* end::in-expr[] */

/* tag::within-expr[] */
within-expr ::= search-expr 'NOT'? 'WITHIN' target-expr
/* end::within-expr[] */

/* tag::range-cond[] */
range-cond ::= ( ( 'ANY' | 'SOME' ) ( 'AND' 'EVERY' )? | 'EVERY' )
               range 'SATISFIES' cond 'END'
/* end::range-cond[] */

/* tag::range-xform[] */
range-xform ::= ( ( 'ARRAY' | 'FIRST' ) | 'OBJECT' name-expr ':' ) var-expr
                'FOR' range ( 'WHEN' cond )? 'END'
/* end::range-xform[] */

/* tag::range[] */
range ::= ( name-var ':' )? var ( 'IN' | 'WITHIN' ) expr
    ( ',' ( name-var ':' )? var ( 'IN' | 'WITHIN' ) expr )*
/* end::range[] */

search-expr ::= expr
target-expr ::= expr
var-expr ::= expr
name-expr ::= expr
var ::= identifier
name-var ::= identifier


/* Construction Expressions */

construction-expr ::= object | array

/* tag::object[] */
object ::= '{' ( ( name-expr ':' )? expr (',' ( name-expr ':' )? expr)* )? '}'
/* end::object[] */

/* tag::array[] */
array ::= '[' ( expr ( ',' expr )* )? ']'
/* end::array[] */


/* Nested Expressions */

/* tag::nested-expr[] */
nested-expr ::= field-expr | element-expr | slice-expr
/* end::nested-expr[] */

/* tag::field-expr[] */
field-expr ::= expr '.' (identifier | (escaped-identifier 'i'?))
/* end::field-expr[] */

/* tag::element-expr[] */
element-expr ::= expr '[' expr ']'
/* end::element-expr[] */

/* tag::slice-expr[] */
slice-expr ::= expr '[' expr ':' expr? ']'
/* end::slice-expr[] */


/* Function Calls */

function-call ::= ordinary-function |
                  aggregate-function |
                  window-function

function-name ::= identifier
aggregate-function-name ::= identifier
window-function-name ::= identifier

ordinary-function ::= function-name '(' ( expr ( ',' expr )* )? ')'

/* tag::aggregate-function[] */
aggregate-function ::= aggregate-function-name '(' ( aggregate-quantifier? expr |
                       ( path '.' )? '*' ) ')' filter-clause? over-clause?
/* end::aggregate-function[] */

/* tag::aggregate-quantifier[] */
aggregate-quantifier ::= 'ALL' | 'DISTINCT'
/* end::aggregate-quantifier[] */

/* tag::filter-clause[] */
filter-clause ::= 'FILTER' '(' 'WHERE' cond ')'
/* end::filter-clause[] */

/* tag::window-function[] */
window-function ::= window-function-name '(' window-function-arguments ')'
                    window-function-options? over-clause
/* end::window-function[] */

/* tag::window-function-arguments[] */
window-function-arguments ::= ( expr ( ',' expr ( ',' expr )? )? )?
/* end::window-function-arguments[] */

/* tag::window-function-options[] */
window-function-options ::= nth-val-from? nulls-treatment?
/* end::window-function-options[] */

/* tag::nthval-from[] */
nthval-from ::= 'FROM' ( 'FIRST' | 'LAST' )
/* end::nthval-from[] */

/* tag::nulls-treatment[] */
nulls-treatment ::= ( 'RESPECT' | 'IGNORE' ) 'NULLS'
/* end::nulls-treatment[] */

/* tag::over-clause[] */
over-clause ::= 'OVER' ( '(' window-definition ')' | window-ref )
/* end::over-clause[] */


/* Subquery Expressions */

/* tag::subquery-expr[] */
subquery-expr ::= '(' select ')'
/* end::subquery-expr[] */


/************
 * Comments *
 ************/

comment ::= block-comment | line-comment

/* tag::block-comment[] */
block-comment ::= '/*' ( text | newline )* '*/'
/* end::block-comment[] */

/* tag::line-comment[] */
line-comment ::=  '--' text? newline?
/* end::line-comment[] */
