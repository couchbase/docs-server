/* Data Definition Language */

ddl-statement ::= create-statement
                | drop-statement
                | other-statement


create-statement ::= create-scope
                   | create-collection
                   | create-primary-index
                   | create-index
                   | create-function

drop-statement ::= drop-scope
                 | drop-collection
                 | drop-primary-index
                 | drop-index
                 | drop-function

other-statement ::= alter-index
                  | build-index
                  | execute-function


/**************************
 * Scopes and Collections *
 **************************/

/* tag::create-scope[] */
create-scope ::= 'CREATE' 'SCOPE' ( namespace ':' )? bucket '.' scope
/* end::create-scope[] */

/* tag::create-collection[] */
create-collection ::= 'CREATE' 'COLLECTION' ( ( namespace ':' )? bucket '.' scope '.' )? collection
/* end::create-collection[] */

/* tag::drop-scope[] */
drop-scope ::= 'DROP' 'SCOPE' ( namespace ':' )? bucket '.' scope
/* end::drop-scope[] */

/* tag::drop-collection[] */
drop-collection ::= 'DROP' 'COLLECTION' ( ( namespace ':' )? bucket '.' scope '.' )? collection
/* end::drop-collection[] */


/***********
 * Indexes *
 ***********/

/* tag::create-primary-index[] */
create-primary-index ::= 'CREATE' 'PRIMARY' 'INDEX' index-name? 'ON' keyspace-ref
                         index-using? index-with?
/* end::create-primary-index[] */

/* tag::index-using[] */
index-using ::= 'USING' 'GSI'
/* end::index-using[] */

/* tag::index-with[] */
index-with ::= 'WITH' expr
/* end::index-with[] */

/* tag::create-index[] */
create-index ::= /* FIXME: */ 'CREATE' 'INDEX' index-name 'ON' keyspace-ref
                 '(' index-key index-order? ( ',' index-key index-order? )* ')'
                 index-partition? where-clause? index-using? index-with?
/* end::create-index[] */

/* tag::index-key[] */
index-key ::= expr
            | array-expr
/* end::index-key[] */

/* tag::index-order[] */
index-order ::= 'ASC' | 'DESC'
/* end::index-order[] */

/* tag::array-expr[] */
array-expr ::= full-array-expr | simple-array-expr
/* end::array-expr[] */

/* tag::full-array-expr[] */
full-array-expr ::= ( 'ALL' | 'DISTINCT' ) 'ARRAY' var-expr 'FOR' var ( 'IN' | 'WITHIN' ) expr ( ','
                    var ( 'IN' | 'WITHIN' ) expr )* ( 'WHEN' cond )? 'END'
/* end::full-array-expr[] */

/* tag::simple-array-expr[] */
simple-array-expr ::= ( 'ALL' | 'DISTINCT' ) expr
/* end::simple-array-expr[] */

/* tag::pairs-function[] */
pairs-function ::= 'PAIRS' '(' ( 'SELF' | index-key-object ) ')'
/* end::pairs-function[] */

/* tag::index-key-object[] */
index-key-object ::= /* FIXME: */ '{' ('"' name '"' ':')? expr (',' ('"' name '"' ':')? expr )* '}'
/* end::index-key-object[] */

/* tag::index-partition[] */
index-partition ::= 'PARTITION' 'BY' 'HASH' '(' partition-key-expr ( ',' partition-key-expr )* ')'
/* end::index-partition[] */

/* tag::partition-key-expr[] */
partition-key-expr ::= expr
/* end::partition-key-expr[] */

/* tag::alter-index[] */
alter-index ::= 'ALTER' 'INDEX' ( index-path '.' index-name | index-name 'ON' keyspace-ref )
                index-using? index-with
/* end::alter-index[] */

/* tag::build-index[] */
build-index ::= 'BUILD' 'INDEX' 'ON' keyspace-ref '(' index-term (',' index-term)* ')' index-using?
/* end::build-index[] */

/* tag::index-term[] */
index-term ::= index-name | index-expr | subquery-expr
/* end::index-term[] */

/* tag::index-expr[] */
index-expr ::= string-literal | array-constructor
/* end::index-expr[] */

/* tag::drop-primary-index[] */
drop-primary-index ::= 'DROP' 'PRIMARY' 'INDEX' 'ON' keyspace-ref index-using?
/* end::drop-primary-index[] */

/* tag::drop-index[] */
drop-index ::= 'DROP' 'INDEX' ( index-path '.' index-name | index-name 'ON' keyspace-ref ) index-using?
/* end::drop-index[] */

/* tag::index-path[] */
index-path ::= keyspace-full | keyspace-prefix | keyspace-partial
/* end::index-path[] */

/* tag::keyspace-full[] */
keyspace-full ::= namespace ':' bucket '.' scope '.' collection
/* end::keyspace-full[] */

/* tag::keyspace-prefix[] */
keyspace-prefix ::= ( namespace ':' )? bucket
/* end::keyspace-prefix[] */


/*************
 * Functions *
 *************/

/* tag::create-function[] */
create-function ::= create-function-inline | create-function-external
/* end::create-function[] */

/* tag::create-function-inline[] */
create-function-inline ::= 'CREATE' ( 'OR' 'REPLACE' )? 'FUNCTION' function '(' params? ')'
                            ( 'IF' 'NOT' 'EXISTS' )? ( '{' body '}' | 'LANGUAGE' 'INLINE' 'AS' body )
/* end::create-function-inline[] */

/* tag::create-function-external[] */
create-function-external ::= 'CREATE' ( 'OR' 'REPLACE' )? 'FUNCTION' function '(' params? ')'
                              ( 'IF' 'NOT' 'EXISTS' )? 'LANGUAGE' 'JAVASCRIPT' 'AS' obj 'AT' library
/* end::create-function-external[] */

/* tag::function[] */
function ::= identifier
           | namespace ':' identifier
           | namespace ':' bucket '.' scope '.' identifier
/* end::function[] */

/* tag::params[] */
params ::= identifier ( "," identifier )* | "..."
/* end::params[] */

/* tag::body[] */
body ::= expr
/* end::body[] */

/* tag::obj[] */
obj ::= string-literal
/* end::obj[] */

/* tag::library[] */
library ::= string-literal
/* end::library[] */

/* tag::drop-function[] */
drop-function ::= 'DROP' 'FUNCTION' function ( 'IF' 'EXISTS' )?
/* end::drop-function[] */

/* tag::execute-function[] */
execute-function ::= 'EXECUTE' 'FUNCTION' function '(' ( expr ( ',' expr )* )? ')'
/* end::execute-function[] */
