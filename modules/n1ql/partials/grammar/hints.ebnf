/* Hints */

/* tag::hint-comment[] */
hint-comment ::= block-hint-comment | line-hint-comment
/* end::hint-comment[] */

/* tag::block-hint-comment[] */
block-hint-comment ::= '/*' '+' hints '*/'
/* end::block-hint-comment[] */

/* tag::line-hint-comment[] */
line-hint-comment ::= '--' '+' hints
/* end::line-hint-comment[] */

/* tag::hints[] */
hints ::= simple-hint-sequence | json-hint-object
/* end::hints[] */

/* tag::simple-hint-sequence[] */
simple-hint-sequence ::= simple-hint+
/* end::simple-hint-sequence[] */

/* tag::simple-hint[] */
simple-hint ::= ordered-hint-simple
              | gsi-hint-simple
              | fts-hint-simple
              | hash-hint-simple
              | nl-hint-simple
/* end::simple-hint[] */

/* tag::ordered-hint-simple[] */
ordered-hint-simple ::= 'ORDERED'
/* end::ordered-hint-simple[] */

/* tag::gsi-hint-simple[] */
gsi-hint-simple ::= ( 'INDEX' '(' keyspace index* ')' )+
/* end::gsi-hint-simple[] */

/* tag::fts-hint-simple[] */
fts-hint-simple ::= ( 'INDEX_FTS' '(' keyspace index* ')' )+
/* end::fts-hint-simple[] */

/* tag::nl-hint-simple[] */
nl-hint-simple ::= 'USE_NL' '(' ( keyspace )+ ')'
/* end::nl-hint-simple[] */

/* tag::hash-hint-simple[] */
hash-hint-simple ::= 'USE_HASH' '(' ( keyspace ( '/' ( 'BUILD' | 'PROBE' ) )? )+ ')'
/* end::hash-hint-simple[] */

/* tag::json-hint-object[] */
json-hint-object ::= '{' json-hint (',' json-hint )* '}'
/* end::json-hint-object[] */

/* tag::json-hint[] */
json-hint ::= ordered-hint-json
             | gsi-hint-json
             | fts-hint-json
             | hash-hint-json
             | nl-hint-json
/* end::json-hint[] */

/* tag::ordered-hint-json[] */
ordered-hint-json ::= '"' 'ordered' '"' ':' 'true'
/* end::ordered-hint-json[] */

/* tag::gsi-hint-json[] */
gsi-hint-json ::= '"' 'index' '"' ':' ( index-array | index-object )
/* end::gsi-hint-json[] */

/* tag::fts-hint-json[] */
fts-hint-json ::= '"' 'index_fts' '"' ':' ( index-array | index-object )
/* end::fts-hint-json[] */

/* tag::nl-hint-json[] */
nl-hint-json ::= '"' 'use_nl' '"' ':' ( keyspace-array | keyspace-object )
/* end::nl-hint-json[] */

/* tag::hash-hint-json[] */
hash-hint-json ::= '"' 'use_hash' '"' ':' ( hash-array | hash-object )
/* end::hash-hint-json[] */

/* tag::index-array[] */
index-array ::= '[' index-object ( ',' index-object )* ']'
/* end::index-array[] */

/* tag::index-object[] */
index-object ::= '{' keyspace-property ',' indexes-property '}'
/* end::index-object[] */

/* tag::indexes-property[] */
indexes-property ::= '"' 'indexes' '"' ':' ( 'null'
                                           | '"' index '"'
                                           | '[' '"' index '"' ( ',' '"' index '"' )* ']' )
/* end::indexes-property[] */

/* tag::keyspace-array[] */
keyspace-array ::= '[' keyspace-object ( ',' keyspace-object )* ']'
/* end::keyspace-array[] */

/* tag::keyspace-object[] */
keyspace-object ::= '{' keyspace-property '}'
/* end::keyspace-object[] */

/* tag::keyspace-property[] */
keyspace-property ::= '"' ( 'keyspace' | 'alias' ) '"' ':' '"' keyspace '"'
/* end::keyspace-property[] */

/* tag::hash-array[] */
hash-array ::= '[' hash-object ( ',' hash-object )* ']'
/* end::hash-array[] */

/* tag::hash-object[] */
hash-object ::= '{' keyspace-property ( "," option-property )? '}'
/* end::hash-object[] */

/* tag::option-property[] */
option-property ::= '"' 'option' '"' ':' ( '"' 'build' '"' | '"' 'probe' '"' | 'null' )
/* end::option-property[] */
