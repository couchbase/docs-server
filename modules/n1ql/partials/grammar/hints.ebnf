/* Hints */

/* tag::hint[] */
hint ::= simple-hint | json-hint
/* end::hint[] */

/* tag::simple-hint[] */
simple-hint ::= ( ordered-hint-simple
                | gsi-hint-simple
                | fts-hint-simple
                | hash-hint-simple
                | nl-hint-simple )+
/* end::simple-hint[] */

/* tag::ordered-hint-simple[] */
ordered-hint-simple ::= 'ORDERED'
/* end::ordered-hint-simple[] */

/* tag::gsi-hint-simple[] */
gsi-hint-simple ::= ( 'INDEX' '(' keyspace index* ')' )+
/* end::gsi-hint-simple[] */

/* tag::fts-hint-simple[] */
fts-hint-simple ::= ( 'INDEX_FTS' '(' keyspace index* ')' )+
/* end::fts-hint-simple[] */

/* tag::nl-hint-simple[] */
nl-hint-simple ::= 'USE_NL' '(' ( keyspace )+ ')'
/* end::nl-hint-simple[] */

/* tag::hash-hint-simple[] */
hash-hint-simple ::= 'USE_HASH' '(' ( keyspace ( '/' ( 'BUILD' | 'PROBE' ) )? )+ ')'
/* end::hash-hint-simple[] */

/* tag::json-hint[] */
json-hint ::= '{' ( ordered-hint-pair
                 | gsi-hint-pair
                 | fts-hint-pair
                 | hash-hint-pair
                 | nl-hint-pair )
                 ( ',' ( ordered-hint-pair
                       | gsi-hint-pair
                       | fts-hint-pair
                       | hash-hint-pair
                       | nl-hint-pair) )* '}'
/* end::json-hint[] */

/* tag::ordered-hint-pair[] */
ordered-hint-pair ::= '"' 'ordered' '"' ':' 'true'
/* end::ordered-hint-pair[] */

/* tag::gsi-hint-pair[] */
gsi-hint-pair ::= '"' 'index' '"' ':' ( index-array | index-object )
/* end::gsi-hint-pair[] */

/* tag::fts-hint-pair[] */
fts-hint-pair ::= '"' 'index_fts' '"' ':' ( index-array | index-object )
/* end::fts-hint-pair[] */

/* tag::nl-hint-pair[] */
nl-hint-pair ::= '"' 'use_nl' '"' ':' ( keyspace-array | keyspace-object )
/* end::nl-hint-pair[] */

/* tag::hash-hint-pair[] */
hash-hint-pair ::= '"' 'use_hash' '"' ':' ( hash-array | hash-object )
/* end::hash-hint-pair[] */

/* tag::index-array[] */
index-array ::= '[' index-object ( ',' index-object )* ']'
/* end::index-array[] */

/* tag::index-object[] */
index-object ::= '{' keyspace-pair ',' index-pair '}'
/* end::index-object[] */

/* tag::index-pair[] */
index-pair ::= '"' 'indexes' '"' ':' ( 'null'
                                     | '"' index '"'
                                     | '[' '"' index '"' ( ',' '"' index '"' )* ']' )
/* end::index-pair[] */

/* tag::keyspace-array[] */
keyspace-array ::= '[' keyspace-object ( ',' keyspace-object )* ']'
/* end::keyspace-array[] */

/* tag::keyspace-object[] */
keyspace-object ::= '{' keyspace-pair '}'
/* end::keyspace-object[] */

/* tag::keyspace-pair[] */
keyspace-pair ::= '"' ( 'keyspace' | 'alias' ) '"' ':' '"' keyspace '"'
/* end::keyspace-pair[] */

/* tag::hash-array[] */
hash-array ::= '[' hash-object ( ',' hash-object )* ']'
/* end::hash-array[] */

/* tag::hash-object[] */
hash-object ::= '{' keyspace-pair ( "," option-pair )? '}'
/* end::hash-object[] */

/* tag::option-pair[] */
option-pair ::= '"' 'option' '"' ':' ( '"' 'build' '"' | '"' 'probe' '"' | 'null' )
/* end::option-pair[] */
